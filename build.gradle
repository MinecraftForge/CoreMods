import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'idea'
    id 'eclipse'
    id 'java-library'
    id 'maven-publish'
    id 'net.minecraftforge.gradleutils' version '[2.1.3,2.3.0)'
}

group 'net.minecraftforge'
version = gradleutils.tagOffsetVersion
println "Version: $version"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
    mavenLocal()
}

changelog {
    fromTag "1.0.0"
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
}

dependencies {
    // TODO: Cleanup tests
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.+')
    testImplementation('org.powermock:powermock-core:2.0.+')
    testImplementation('org.hamcrest:hamcrest-core:2.2')
    testImplementation('org.apache.logging.log4j:log4j-core:2.19.0')
    testImplementation(libs.modlauncher)
    testImplementation(libs.forgespi)
    testImplementation(libs.unsafe)
    testCompileOnly('org.jetbrains:annotations:21.0.1')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.7.+')
    testRuntimeOnly(project(':coremods-test-jar'))

    compileOnly(libs.modlauncher)
    compileOnly(libs.securemodules)
    implementation(libs.log4j.api)
    api(libs.bundles.asm)
    compileOnly(libs.forgespi)
    implementation(libs.nashorn)
    compileOnly(libs.nulls)
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Specification-Title': 'coremods',
            'Specification-Vendor': 'Forge Development LLC',
            'Specification-Version': '1', // TODO: Use the tag
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor' :'Forge Development LLC'
        ], 'net/minecraftforge/coremod/')
    }
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        pom {
            from components.java
            artifactId = 'coremods'
            name = 'Core Mods'
            description = 'JavaScript based core modding framework for use with Forge'
            url = 'https://github.com/MinecraftForge/CoreMods'
            PomUtils.setGitHubDetails(pom, 'CoreMods')
            license PomUtils.Licenses.LGPLv2_1
            
            developers {
                developer PomUtils.Developers.LexManos
                developer PomUtils.Developers.cpw
            }
        }
    }
    
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

allprojects {
    ext.VALID_VMS = [
        'Adoptium':  (17..21),
        'Amazon':    (17..21),
        'Azul':      (17..21),
        'BellSoft':  (17..21),
        'Graal_VM':  [17,     19, 20, 21],
        'IBM':       [17, 18, 19, 20    ],
        'Microsoft': [17,             21],
        'Oracle':    (17..21),
        'SAP':       (17..20)
    ]
    ext.VALID_VMS = [ 'Adoptium':  [17] ]
}

idea.module {
    downloadJavadoc = downloadSources = true
}
