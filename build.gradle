import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'idea'
    id 'eclipse'
    id 'java-library'
    id 'maven-publish'
    id 'net.minecraftforge.gradleutils' version '[2.1.2,2.3.0)'
}

group 'net.minecraftforge'
version = gradleutils.tagOffsetVersion
println("Version: $version")

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
    mavenLocal()
}

changelog {
    fromTag "1.0.0"
}

license {
    header = file('LICENSE-header.txt')
    newLine = false
}

dependencies {
    // TODO: Cleanup tests
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.+')
    testImplementation('org.powermock:powermock-core:2.0.+')
    testImplementation('org.hamcrest:hamcrest-core:2.2')
    testImplementation('org.apache.logging.log4j:log4j-core:2.19.0')
    testCompileOnly('org.jetbrains:annotations:21.0.1')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.7.+')
    testRuntimeOnly(project(':coremods-test-jar'))
    
    compileOnly(libs.modlauncher)
    implementation(libs.log4j.api)
    api(libs.bundles.asm)
    compileOnly(libs.forgespi)
    implementation(libs.nashorn)
    compileOnly(libs.nulls)
    
    
    annotationProcessor(implementation('org.bsc.processor:java2ts-processor:1.3.1'))
    
    // there are here because for some reason, java2ts decides to load the classes directly
    //      instead of using the built-in AP feature to do proper checking
    annotationProcessor(libs.bundles.asm)
    annotationProcessor('org.ow2.asm:asm-util:7.3.1') // for processor
    // TODO automate moving files from build/generated/sources/annotationProcessor/java/main/j2ts to resources out
    //      also automate doing ASMAPI class. you can't exactly load the class while compiling the class
    //      and also the `declare namespace Java { export function type(s: "class.name"): class.name; }` stuff, I just used some regex
    
}



jar {
    manifest {
        attributes([
            'Specification-Title': 'coremods',
            'Specification-Vendor': 'Forge Development LLC',
            'Specification-Version': '1', // TODO: Use the tag
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor' :'Forge Development LLC'
        ] as java.util.LinkedHashMap, 'net/minecraftforge/coremod/')
    }
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        pom {
            from components.java
            artifactId = 'coremods'
            name = 'Core Mods'
            description = 'JavaScript based core modding framework for use with Forge'
            url = 'https://github.com/MinecraftForge/CoreMods'
            PomUtils.setGitHubDetails(pom, 'CoreMods')
            license PomUtils.Licenses.LGPLv2_1
            
            developers {
                developer PomUtils.Developers.LexManos
                developer PomUtils.Developers.cpw
            }
        }
    }
    
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

allprojects {
    ext.VALID_VMS = [
        'Adoptium':  [16, 17, 18, 19, 20, 21],
        'Amazon':    [16, 17, 18, 19, 20, 21],
        'Azul':      (16..21),
        'BellSoft':  (16..21),
        'Graal_VM':  [16, 17,     19, 20, 21],
        'IBM':       [16, 17, 18, 19, 20    ],
        'Microsoft': [16, 17,             21],
        'Oracle':    (16..21),
        'SAP':       (16..20)
    ]
    ext.VALID_VMS = [ 'Adoptium':  [16] ]
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}