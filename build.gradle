
plugins {
    id 'org.ajoberstar.reckon' version '0.9.0'
    id 'org.ajoberstar.grgit' version '3.0.0'
    id "com.github.ben-manes.versions" version '0.20.0'
}
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

group 'net.minecraftforge'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jacoco {
    toolVersion = "0.8.2"
}

reckon {
    scopeFromProp()
    stageFromProp('milestone', 'rc', 'final')
}

reckonTagCreate.dependsOn check

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.4.+")
    testImplementation("org.powermock:powermock-core:2.0.+")
    testImplementation("org.hamcrest:hamcrest-core:2.1")
    testImplementation("org.apache.logging.log4j:log4j-core:2.11.+")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.4.+")
    implementation("cpw.mods:modlauncher:1.0.+")
    implementation("org.apache.logging.log4j:log4j-api:2.11.+")
    implementation("org.ow2.asm:asm:6.2")
    implementation("org.ow2.asm:asm-commons:6.2")
    implementation("org.ow2.asm:asm-tree:6.2")
    implementation("net.minecraftforge:forgespi:0.7.+")
    implementation("com.google.code.findbugs:jsr305:3.0.2")
}

ext.sharedManifest = manifest {
    attributes(["Specification-Title": "coremods",
                "Specification-Vendor": "Forge",
                "Specification-Version": "1", // Currently version 1 of the coremod specification
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}+${System.getenv("BUILD_NUMBER")?:0}+${grgit.head().abbreviatedId}",
                "Implementation-Vendor" :"Forge",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Git-Commit": grgit.head().abbreviatedId,
                "Git-Branch": grgit.branch.current().getName() ],
            "net/minecraftforge/coremod/")
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
}

sourceSets {
    testJars
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                from components.java
                artifact sourcesJar
                name = 'Core Mods'
                description = 'Core modding framework for use with forge'
                url = 'https://github.com/MinecraftForge/coremods'
                scm {
                    url = 'https://github.com/MinecraftForge/coremods'
                    connection = 'scm:git:git://github.com/MinecraftForge/coremods.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/coremods.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/coremods/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.properties.forgeMavenUser?:'fake'
                password project.properties.forgeMavenPassword?:'news'
            }
            url "http://files.minecraftforge.net/maven/manage/upload"
        }
    }
}