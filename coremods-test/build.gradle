plugins {
    id 'eclipse'
    id 'java-library'
    alias libs.plugins.license
    alias libs.plugins.modules
    //alias libs.plugins.versions
    alias libs.plugins.gradleutils
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
    mavenLocal()
}

java {
    toolchain.languageVersion = rootProject.java.toolchain.languageVersion
}

license {
    header = rootProject.file("LICENSE-header.txt")
    newLine = false
}

test {
    useJUnitPlatform()
    reports.html.outputLocation = rootProject.file("build/reports/")
    reports.junitXml.outputLocation = rootProject.file("build/test-results/")
}

test {
    //exclude '**/*'
    useJUnitPlatform()
}

compileTestJava {
    //exclude '**/*'
}

sourceSets {
    test {
        resources {
            srcDir 'src/test/javascript/'
        }
    }
}

dependencies {
    testImplementation(rootProject)
    testImplementation(project(':coremods-test-jar'))
    testImplementation(libs.junit.api)
    testImplementation(libs.log4j.api)
    testImplementation(libs.modlauncher)
    testImplementation(libs.securemodules)
    testImplementation(libs.forgespi)
    testImplementation(libs.unsafe)
    testRuntimeOnly(libs.bundles.junit.runtime)
    testCompileOnly(libs.nulls)
}

extraJavaModuleInfo {
    failOnMissingModuleInfo = false
    automaticModule('jopt-simple-5.0.4.jar', 'jopt.simple')
}

// If we are being told a specific vendor then we are probably being run in parallel
if (project.hasProperty('javaVendor') && project.hasProperty('javaVersion')) {
    test.javaLauncher.set(javaToolchains.launcherFor {
        it.vendor.set(JvmVendorSpec."${project.property('javaVendor').toUpperCase(Locale.ROOT)}" as JvmVendorSpec)
        it.languageVersion.set(JavaLanguageVersion.of(project.property('javaVersion') as int))
        it.implementation.set(JvmImplementation.VENDOR_SPECIFIC)
    })
} else if (!project.hasProperty('disable_bulk_tests')) {
    configurations {
        groovyScript
    }

    dependencies {
        groovyScript libs.ivy
        groovyScript libs.groovy
    }

    tasks.register('collectTests', JavaExec) {
        classpath = configurations.groovyScript
        mainClass = 'groovy.ui.GroovyMain'
        args '.github/workflows/aggregate-junit-tests.groovy'
        workingDir rootProject.projectDir
    }

    VALID_VMS.each { javaVendor, javaVersions ->
        javaVersions.each { javaVersion ->
            def task = tasks.register("test${javaVendor}${javaVersion}", Test) {
                testClassesDirs = testing.suites.test.sources.output.classesDirs
                classpath = testing.suites.test.sources.runtimeClasspath
                useJUnitPlatform()
                javaLauncher.set(javaToolchains.launcherFor {
                    it.vendor.set(JvmVendorSpec."${javaVendor.toUpperCase(Locale.ROOT)}" as JvmVendorSpec)
                    it.languageVersion.set(JavaLanguageVersion.of(javaVersion))
                    it.implementation.set(JvmImplementation.VENDOR_SPECIFIC)
                })
                reports.html.outputLocation = rootProject.file("build/test_artifacts/test-reports-${javaVendor}-${javaVersion}/")
                reports.junitXml.outputLocation = rootProject.file("build/test_artifacts/test-results-${javaVendor}-${javaVersion}/")
            }
            test.dependsOn(task)
            collectTests.mustRunAfter(task)
        }
    }
}

// Hack eclipse into knowing that the gradle deps are modules
eclipse.classpath {
    containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
    file.whenMerged { entries.findAll { it.kind == 'lib' || it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }.each { it.entryAttributes['module'] = 'true' } }
}
